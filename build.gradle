plugins {
  id("com.kazurayam.compare-directories") version "0.2.13"
}

ext {
    KATALONSTUDIO_INSTALLATION_VERSION = '10.4.2'
    KATALONSTUDIO_INSTALLATION_DIR = '/Applications/Katalon Studio Enterprise.app/Contents/Eclipse'

    COMPARE_VERSION_A = '8.6.9'
    COMPARE_VERSION_B = '10.4.2'
    KATALONSTUDIO_SOURCE_DIR = "${KATALONSTUDIO_INSTALLATION_DIR}/configuration/resources/source"
    KATALONSTUDIO_EXTENSIONS_DIR = "${KATALONSTUDIO_INSTALLATION_DIR}/configuration/resources/extensions"
    KATALONSTUDIO_HTML_DIR = "${KATALONSTUDIO_INSTALLATION_DIR}/configuration/resources/html"
    VERSIONS_DIR = layout.projectDirectory.dir('versions')
}

tasks.register('extractVersion') {
    doFirst {
        println "KATALONSTUDIO_INSTALLATION_DIR: ${KATALONSTUDIO_INSTALLATION_DIR}"
        println "KATALONSTUDIO_INSTALLATION_VERSION: ${KATALONSTUDIO_INSTALLATION_VERSION}"
        println "VERSIONS_DIR: ${VERSIONS_DIR}"
    }
    doFirst {
        delete VERSIONS_DIR.dir("${KATALONSTUDIO_INSTALLATION_VERSION}")
        mkdir  VERSIONS_DIR.dir("${KATALONSTUDIO_INSTALLATION_VERSION}")
    }
    doLast {
        copy {
            from zipTree("${KATALONSTUDIO_SOURCE_DIR}/com.kms.katalon.core/com.kms.katalon.core-sources.jar")
            into VERSIONS_DIR.dir("${KATALONSTUDIO_INSTALLATION_VERSION}/resources/source/com.kms.katalon.core")
        }
        copy {
            from zipTree("${KATALONSTUDIO_SOURCE_DIR}/com.kms.katalon.core.mobile/com.kms.katalon.core.mobile-sources.jar")
            into VERSIONS_DIR.dir("${KATALONSTUDIO_INSTALLATION_VERSION}/resources/source/com.kms.katalon.core.mobile")
        }
        copy {
            from zipTree("${KATALONSTUDIO_SOURCE_DIR}/com.kms.katalon.core.webservice/com.kms.katalon.core.webservice-sources.jar")
            into VERSIONS_DIR.dir("${KATALONSTUDIO_INSTALLATION_VERSION}/resources/source/com.kms.katalon.core.webservice")
        }
        copy {
            from zipTree("${KATALONSTUDIO_SOURCE_DIR}/com.kms.katalon.core.webui/com.kms.katalon.core.webui-sources.jar")
            into VERSIONS_DIR.dir("${KATALONSTUDIO_INSTALLATION_VERSION}/resources/source/com.kms.katalon.core.webui")
        }
        // Here I ignore the jars of cucumber, mobile, testng, webservice, windows as I am not interested in them
    }
    doLast {
        copy {
            from(KATALONSTUDIO_EXTENSIONS_DIR) {
                include 'scripts/smart-wait.js'
            }
            into VERSIONS_DIR.dir("${KATALONSTUDIO_INSTALLATION_VERSION}/resources/extentions")
        }
        copy {
             from(KATALONSTUDIO_HTML_DIR)
             into VERSIONS_DIR.dir("${KATALONSTUDIO_INSTALLATION_VERSION}/resources/html")
        }
    }
}

tasks.register("compareVersions", com.kazurayam.dircomp.CompareDirectoriesTask) {
    dirA = fileTree(VERSIONS_DIR.dir(COMPARE_VERSION_A)) { exclude "**/*.zip" }
    dirB = fileTree(VERSIONS_DIR.dir(COMPARE_VERSION_B)) { exclude "**/*.zip" }
    def outDir = layout.buildDirectory.dir("compareVersions/${COMPARE_VERSION_A}_${COMPARE_VERSION_B}").get()
    outputFile = outDir.file("differences.json")
    diffDir = outDir.dir("diff")
    doFirst {
        delete outDir
    }
    doLast {
        println "output at " + outDir
    }
}
